---
page_title: 'Import: collection'
sidebar_current: docs-imports-collection
description: The collection import provides useful helpers for working with maps and lists.
layout: docs
---

# Import: collection

The `collection` import provides helpers for working with [maps](/sentinel/language/maps) and [lists](/sentinel/language/lists).

## filter

**_filter(items, predicate)_**

Calls [predicate](#predicates) for each element in collection, returning a list of elements that the
predicate __does__ return true for.

### Arguments

| Name      | Description |
|-----------|-------------|
| items     | the list or map to perform the filter against. |
| predicate | a [predicate](#predicates) function to call for each element in the collection. A response of `true` will add the element to the result list. |

### Examples

Return all even numbers:

```sentinel playground
import "collection"

items = [2, 3, 4, 5, 6, 7, 8]
result = collection.filter(items, func(el) {
  return el % 2 is 0
})
main = result is [2, 4, 6, 8]
```

## find

**_find(items, predicate)_**

Find and return an element within a collection according to the provided [predicate](#predicates). If nothing is found, returns [undefined](/sentinel/language/undefined).

### Arguments

| Name      | Description |
|-----------|-------------|
| items     | the list or map to perform the find against. |
| predicate | a [predicate](#predicates) function to call for each element in the collection. A response of `true` will return the element and complete the find. |

### Examples

Find an element in a collection based on its value:

```sentinel playground
import "collection"

items = ["foo", "bar", "qux"]
item = collection.find(items, func(el) {
  return el is "bar"
})
main = item is "bar"
```

Find an element in a collection based on its key:

```sentinel playground
import "collection"

items = {"foo": 2, "bar": 4}
item = collection.find(items, func(el, key) {
  return key is "bar"
})
main = item is 4
```

## matches

**_matches(items, partial)_**

Compare each element in a collection against a partial map using deep comparison. Returns
the list of elements that returned true for the partial comparison. If no matches are found, returns an empty list.

### Arguments

| Name    | Description |
|---------|-------------|
| items   | the list or map to perform the match against. |
| partial | the map used for partial deep comparison against each element in the collection. |

### Examples

Return all items that contain `{"foo": {"bar": "wip"}}`:

```sentinel playground
import "collection"

items = [
  # This item should match
  {
    "foo": { "bar": "wip"},
    "baz": "qux",
  },
  # This item will not match
  {
    "foo": "bar",
    "baz": "bar",
   },
]
result = collection.matches(items, {"foo": {"bar": "wip"}})
main = result is [{"foo": {"bar": "wip"}, "baz": "qux"}]
```

## reduce

**_reduce(items, accumulator[, initial])_**

Call an accumulator function for each element in collection, supplying the
previous accumulated value as the accumulation parameter.

### Arguments

| Name        | Description |
|-------------|-------------|
| items       | the list or map to perform the reduce against. |
| accumulator | a function that is called for each element in the collection, used to accumulate the value. The first argument is the current accumulated value, the remaining arguments use the same rules as [predicate](#predicates) functions. |
| initial     | the initial value to use. It is an optional argument, and if not provided the first element in the collection is used as the initial value. |

### Examples

Reduce the collection by adding each element together:

```sentinel playground
import "collection"

items = [1, 2, 3, 4, 5]
result = collection.reduce(items, func(acc, el) {
  return acc + el
}, 0)
main = result is 15
```

## reject

**_reject(items, predicate)_**

Calls [predicate](#predicates) for each element in collection, returning a list of elements that the
predicate __does not__ return true for.

### Arguments

| Name      | Description |
|-----------|-------------|
| items     | the list or map to perform the reject against. |
| predicate | a [predicate](#predicates) function to call for each element in the collection. A response of `false` will add the element to the result list. |

### Examples

Return all odd numbers by rejecting even ones:

```sentinel playground
import "collection"

items = [2, 3, 4, 5, 6, 7, 8]
result = collection.reject(items, func(el) {
  return el % 2 is 0
})
main = result is [3, 5, 7]
```

## Predicates

Some helpers accept a predicate function, which has the purpose of making an
assertion. Each predicate may accept differing arguments and return differing
types. If the collection is a list, the parameters will be the element and index.
If the collection is a Map, the parameters will be the value and the key.

### Examples

A predicate for a list of items:

```sentinel
// including index
func(item, index) {
  return true
}

// excluding index
func(item) {
  return true
}
```

A predicate for a map of items:
```sentinel
// including key
func(value, key) {
  return true
}

// excluding key
func(value) {
  return true
}
```
