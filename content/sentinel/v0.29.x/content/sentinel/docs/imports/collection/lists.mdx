---
page_title: 'Import: collection/lists'
sidebar_current: docs-imports-collection-lists
description: The collection/lists import provides useful helpers for working with lists.
layout: docs
---

# Import: collection/lists

The `collection/lists` import provides helpers for working with [lists](/sentinel/language/lists).

## concat

**_concat(items, others[, ...additional])_**

Join multiple lists together, returning the resulting list. This helper must
have at least two arguments supplied. Order is important,
as the order of arguments is the order that lists will be appended.

### Arguments

| Name       | Description |
|------------|-------------|
| items      | the first list to add to the resulting value |
| others     | the second list to add to the resulting value |
| additional | any number of additional lists to add to the resulting value |

### Examples

Concatenate two lists:

```sentinel playground
import "collection/lists" as lists

result = lists.concat([1], [2, 3])
main = result is [1, 2, 3]
```

Concatenate many lists:
```sentinel playground
import "collection/lists" as lists

result = lists.concat([1, 2], [10, 20], [100, 200])
main = result is [1, 2, 10, 20, 100, 200]
```

## difference

**_difference(items[, ...additional])_**

Difference returns a new list with all values that exist in the first list
that are not present in all remaining provided lists.

### Arguments

| Name       | Description |
|------------|-------------|
| items      | the first list, used as the source for ordering |
| additional | any number of additional lists used to subtract from the source list |

### Aliases

__diff__

### Examples

Difference between two lists:

```sentinel playground
import "collection/lists" as lists

result = lists.difference([1, 2], [2, 3])
main = result is [1]
```

Difference between many lists:
```sentinel playground
import "collection/lists" as lists

result = lists.difference([1, 2, 3, 4, 5], [2, 5], [1])
main = result is [3, 4]
```

## intersection

**_intersection(items[, ...additional])_**

Intersection returns a list consisting of unique values that are present in all
of the provided lists.

### Arguments

| Name       | Description |
|------------|-------------|
| items      | the first list to use for intersection |
| additional | any number of additional lists used to intersect |

### Examples

Intersection between three lists:

```sentinel playground
import "collection/lists" as lists

result = lists.intersection([1, 2], [2, 3], [3, 2])
main = result is [2]
```

## sort

**_sort(items, sort_function)_**

Sort will sort a list of elements according to the provided sort function, returning
a new, sorted list.

### Arguments

| Name | Description |
|------|-------------|
| items | the list to sort |
| sort_function | the function that is used to perform the sort |

### Sort Function

The provided sort function accepts two arguments, which can be considered as the
`current` and `next` item. The function should return one of the following values:

| Result | Description |
|--------|-------------|
| -1     | `current` is less than `next` |
| 0      | `current` is equal to `next` |
| +1     | `current` is greater than `next` |

The [compare](/sentinel/functions/compare) built-in method provides a way of
performing comparisons against integers, floats or strings to return a supported
value.

### Examples

Sort a list of words:

```sentinel playground
import "collection/lists" as lists

items = ["zebra", "bat", "horse"]
result = lists.sort(items, func(x, y) {
  return compare(x, y)
})
main = result is ["bat", "horse", "zebra"]
```

Sort a list of objects by a key:

```sentinel playground
import "collection/lists" as lists

items = [{"foo": 8}, {"foo": 4}, {"foo": 100}]
result = lists.sort(items, func(x, y) {
  return compare(x.foo, y.foo)
})
main = result is [{"foo": 4}, {"foo": 8}, {"foo": 100}]
```

## sum

**_sum(items)_**

Sum will add all elements within the provided list. The
list must only contain integers or floats. The return value will always be a float.

### Arguments

| Name | Description |
|------|-------------|
| items | the list to sum |

### Examples

Perform a sum on a list of numbers:

```sentinel playground
import "collection/lists" as lists

items = [2, 5, 10, 500]
main = lists.sum(items) is 517
```

## union

**_union(items[, ...additional])_**

Union will return a list that contains unique values from all provided lists.

### Arguments

| Name       | Description |
|------------|-------------|
| items      | the first list of values |
| additional | any number of additional lists of values |

### Examples

Perform a union on multiple lists:

```sentinel playground
import "collection/lists" as lists

items = lists.union([1, 3], [2, 3, 5], [6, 2, 1])

main = items is [1, 3, 2, 5, 6]
```
