---
page_title: Sentinel Language - Lists
sidebar_title: Lists
sidebar_current: docs-language-lists
description: Lists are a collection of zero or more values.
layout: docs
---

# Language: Lists

Lists are a collection of zero or more values.

Lists can be created using by wrapping values in `[]` and separating them
by commas. An optional trailing comma is allowed. List elements can be
differing types. Examples:

```sentinel
[]                  // An empty list
["foo"]             // Single element list
["foo", 1, 2, true] // Multi element list with different types
["foo", [1, 2]]     // List containing another list
```

A list can be [sliced](/sentinel/language/slices) to create sublists.
The [set operators](/sentinel/language/boolexpr#set-operators) can be used
to test for value inclusion in a list.

## Accessing Elements

List elements can be accessed with the syntax `name[index]` where
`index` is zero-indexed.

A negative index accesses the list in reverse. It is the same as
reversing a list and then using a positive index. Similar to a positive
index, it is bounded by the length of the list as a negative value.

Accessing beyond the length of the list results in
[undefined](/sentinel/language/undefined).

Examples:

```sentinel
list = ["foo", 1, true, [1, 2]]

list[0]    // "foo"
list[2]    // true
list[4]    // undefined
list[-2]   // true
list[-4]   // "foo"
list[-5]   // undefined
list[3][1] // 2
```

## List Append

Values can be appended to a list using the built-in
[append](/sentinel/functions/append) function.

This modifies the list in-place and returns
[undefined](/sentinel/language/undefined). For more information on
why `append` behaves this way, please read the full documentation for the
[append](/sentinel/functions/append) function.

```sentinel
append([1,2], 3)      // [1, 2, 3]
append([1,2], "foo")  // [1, 2, "foo"]
append([1,2], [3])    // [1, 2, [3]]
append(1, 3)          // error()
```

## List Concatenation

Two lists can be concatenated using the `+` operator or the shorthand
`+=` assignment operator. For the `+` operator, a new list is returned.
For `+=`, the left-hand list is modified in place.

Examples:

```sentinel
[1] + [2]   // [1, 2]
[1] + [[1]] // [1, [1]]
[1] + 1     // error

a = [1]
a += [2]    // a = [1, 2]
a += 3      // error
```

## List Length

The length of a list can be retrieved using the
[length](/sentinel/functions/length) function.

Examples:

```sentinel
length([])      // 0
length(["foo"]) // 1
```

## Removing Items From a List

You can use a combination of [list concatenation](#list-concatenation) and
[slicing](/sentinel/language/slices) to remove elements from a list:

```sentinel
a = [0, 1, 2]
a = foo[:1] + foo[2:] // [0, 2]
```

## List Comparison

Lists may be [compared](/sentinel/language/boolexpr#comparison-operators)
for equality. Lists are equal if they are of equal length and their
corresponding elements are comparable and equal. Lists with the same elements
but in a different order are not equal.

```sentinel
[1, 2] is [1, 2]                       // true
[1, 2] is [2, 1]                       // false
["a"] is ["a", "b"]                    // false
["a", ["b", "c"]] is ["a", ["b", "c"]] // true
```

List comparison speed is O(N), meaning that the speed of the comparison is
_linearly_ proportional to the number of elements in the list. The more
elements, the more iterations that are necessary to verify equality.

-> The N-value quoted above should account for the sum of the elements of _all_
lists in the subjects of comparison, as list comparison will recurse into these
lists to check for equality.
