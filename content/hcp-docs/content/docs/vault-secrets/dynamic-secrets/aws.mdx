---
page_title: AWS
sidebar_title: AWS
description: |-
  Describes how to set up dynamic secrets for AWS
---

# AWS dynamic secrets

@include 'alerts/vault-secrets-standard-only.mdx'

HCP Vault Secrets can create short-lived AWS credentials on demand.

## Prerequisites

- Ability to create AWS IAM identity providers, users, and roles
   - Your AWS principal may use the managed [IAMFullAccess](https://docs.aws.amazon.com/aws-managed-policy/latest/reference/IAMFullAccess.html) policy or a custom policy for authorization
- Ability to create HCP Vault Secrets integrations, apps, and secrets

## Set up AWS

HCP Vault Secrets is able to authenticate with your AWS account using two different
methods. Each method requires AWS resources to provision dynamic credentials:

- OpenID Connect (OIDC) Federation (**Recommended**)
  - IAM OIDC identity provider
  - IAM role that HCP can assume through its web identity
  - IAM role with the permissions to grant to the generated dynamic credentials
- Access Keys
  - IAM user with an access key pair
  - IAM role with the permissions to grant to the generated dynamic credentials

Configure your AWS account using either the AWS console or Terraform.

<Tabs>
<Tab heading="Console - OIDC Federation">

### Add identity provider

1. Navigate to the [Add an Identity provider](https://us-east-1.console.aws.amazon.com/iam/home#/identity_providers/create)
  section in the AWS IAM service.

1. Select the **OpenID connect** provider type.

1. Use `https://idp.hashicorp.com/oidc/organization/<org-id>` as the provider URL, replacing the placeholder with your
   **HCP organization ID**.

   <Note>

   You can navigate to the [AWS integration creation
   page](https://portal.cloud.hashicorp.com/services/secrets/integrations/create)
   on the HCP portal and select **AWS** from the list to easily
   find the appropriate Provider URL and org ID.

   </Note>

1. Use `arn:aws:iam::<account-id>:oidc-provider/idp.hashicorp.com/oidc/organization/<org-id>` as the audience, replacing the
   placeholders with your **AWS account ID and HCP organization ID**.

1. Click **Add provider**.

1. Select the identity provider you just created and **note its ARN and Audience** for the next steps.

### Create IAM role for integration

1. Navigate to the [Create
   Role](https://console.aws.amazon.com/iam/home#/roles/create) section
   in the AWS IAM service.

1. Select the **Web Identity** trust entity type.

1. Select the Identity provider and Audience created in the previous step from the dropdown and click **Next**.

1. This role does not need permissions, click **Next**.

1. Give the role a name and optionally a description and tags, then click **Create role**.

1. Select the role you just created and **note its ARN** for the next steps.

### Create IAM role for dynamic secret

1. Navigate again to the [Create
   Role](https://console.aws.amazon.com/iam/home#/roles/create) section
   in the AWS IAM service.

1. Select the **Custom trust policy** trust entity type.

1. Paste the following into the trust policy editor, replacing the
   highlighted placeholder text with the AWS IAM role ARN created earlier, and
   click **Next**.

   <CodeBlockConfig highlight="7">

   ```json
   {
     "Version": "2012-10-17",
     "Statement": [
       {
         "Effect": "Allow",
         "Principal": {
         "AWS": "<iam-role-arn-from-step-2.6>"
       },
       "Action": "sts:AssumeRole",
       "Condition": {}
       }
     ]
   }
   ```

   </CodeBlockConfig>

1. Attach or create a new policy with the permissions to grant to the dynamic
   credentials HCP Vault Secrets will provision.

1. Give the role a name and optionally a description and tags, then click
   **Create role**.

1. Select the role you just created and **note its ARN** for the next steps.

</Tab>
<Tab heading="Console - Access Keys">

### Create IAM user for integration

1. Navigate to the [Create
   User](https://us-east-1.console.aws.amazon.com/iam/home#/users/create) section
   in the AWS IAM service.

1. Give the user a name, click **Next**.

1. This role does not need permissions, click **Next**.

1. Add optional tags and click **Create user**.

1. Select the user you just created and **note its ARN** for the next steps.

1. Click the **Security credentials** for your new user

1. Click **Create access key**

1. Select the **Third-party service** option, check the confirmation box, and click **Next**

1. Add optional tags and click **Create access key**.

1. Take note of the **Access key** and **Secret access key** for the next steps.

<Warning>
  Be cautious with the AWS access key and secret access key while they are in transit
  to HCP Vault Secrets. These credentials provide access to your AWS account and and do
  not automatically expire.
</Warning>

### Create IAM role for dynamic secret

1. Navigate again to the [Create
   Role](https://console.aws.amazon.com/iam/home#/roles/create) section
   in the AWS IAM service.

1. Select the **Custom trust policy** trust entity type.

1. Paste the following into the trust policy editor, replacing the
   highlighted placeholder text with the AWS IAM user ARN created earlier, and
   click **Next**.

   <CodeBlockConfig highlight="7">

   ```json
   {
     "Version": "2012-10-17",
     "Statement": [
       {
         "Effect": "Allow",
         "Principal": {
         "AWS": "<iam-user-arn-from-step-1.6>"
       },
       "Action": "sts:AssumeRole",
       "Condition": {}
       }
     ]
   }
   ```

   </CodeBlockConfig>

1. Attach or create a new policy with the permissions to grant to the dynamic
   credentials HCP Vault Secrets will provision.

1. Give the role a name and optionally a description and tags, then click
   **Create role**.

1. Select the role you just created and **note its ARN** for the next steps.

</Tab>
<Tab heading="Terraform - OIDC Federation">

The Terraform [AWS
provider](https://registry.terraform.io/providers/hashicorp/aws/latest) can be
used to provision the necessary resources.

1. Follow the [Terraform
documentation](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#authentication-and-configuration)
to authenticate the AWS provider.

1. Use your preferred text editor and create a Terraform configuration file with the following snippet:

  ```hcl
  provider "aws" {}

  variable "organization_id" {
    type        = string
    description = "Your HCP organization ID."
  }
  variable "project_id" {
    type        = string
    description = "Your HCP project ID."
  }
  variable "integration_name" {
    type        = string
    description = "Your AWS integration name. Must match the name of the AWS integration you'll eventually create in HCP Vault Secrets."
  }

  data "aws_caller_identity" "current" {}

  locals {
    account_id = data.aws_caller_identity.current.account_id
    issuer     = "idp.hashicorp.com/oidc/organization/${var.organization_id}"
    audience   = "arn:aws:iam::${local.account_id}:oidc-provider/${local.issuer}"
    subject    = "project:${var.project_id}:geo:us:service:vault-secrets:type:integration:name:${var.integration_name}"
  }

  resource "aws_iam_openid_connect_provider" "hcp_vault_secrets" {
    url             = "https://${local.issuer}"
    thumbprint_list = ["9e99a48a9960b14926bb7f3b02e22da2b0ab7280"]
    client_id_list  = [local.audience]
  }

  resource "aws_iam_role" "integration_role" {
    name = "hcp-vault-secrets-integration"
    assume_role_policy = jsonencode({
      Version = "2012-10-17"
      Statement = [
        {
          Action = "sts:AssumeRoleWithWebIdentity"
          Effect = "Allow"
          Principal = {
            Federated = aws_iam_openid_connect_provider.hcp_vault_secrets.arn
          }
          Condition = {
            StringEquals = {
              "${local.issuer}:sub" = local.subject
            }
          }
        }
      ]
    })
  }

  resource "aws_iam_role" "dynamic_secret_role" {
    name = "hcp-vault-secrets-dynamic-secret"
    assume_role_policy = jsonencode({
      "Version" : "2012-10-17",
      "Statement" : [
        {
          "Effect" : "Allow",
          "Action" : "sts:AssumeRole",
          "Principal" : {
            "AWS" : aws_iam_role.integration_role.arn
          },
          "Condition" : {}
        }
      ]
    })

    inline_policy {
      name   = "hcp-vault-secrets-s3-lister"
      policy = data.aws_iam_policy_document.dynamic_secret_policy.json
    }
  }

  data "aws_iam_policy_document" "dynamic_secret_policy" {
    # Replace with the permissions to grant to your dynamic secret
    statement {
      effect = "Allow"
      actions = [
        "s3:List*"
      ]
      resources = ["*"]
    }
  }

  output "integration_name" {
    value = var.integration_name
  }

  output "audience" {
    value = local.audience
  }

  output "integration_role_arn" {
    value = aws_iam_role.integration_role.arn
  }

  output "dynamic_secret_role_arn" {
    value = aws_iam_role.dynamic_secret_role.arn
  }
  ```

1. Initialize Terraform.

  ```shell-session
  $ terraform init
  ```

  This will prepare a working directory:

  <CodeBlockConfig hideClipboard>

    ```plaintext
    ...snip...

    Terraform has been successfully initialized!
    ```
  </CodeBlockConfig>

1. Generate your AWS IAM configuration with the apply command:

  ```shell-session
  $ terraform apply
  ```

  Review the actions Terraform is about to perform and say `yes` to accept the plan:

  <CodeBlockConfig hideClipboard>

    ```plaintext
    ...snip...

    Plan: 3 to add, 0 to change, 0 to destroy.

    Changes to Outputs:
    + dynamic_secret_role_arn = (known after apply)
    + integration_role_arn    = (known after apply)

    Do you want to perform these actions?
    Terraform will perform the actions described above.
    Only 'yes' will be accepted to approve.

    Enter a value: yes
    ```

  </CodeBlockConfig>

1. Keep the Terraform outputs on hand for the next steps:

  <CodeBlockConfig hideClipboard>

    ```plaintext
    Apply complete! Resources: 3 added, 0 changed, 0 destroyed.

    Outputs:

    audience = "arn:aws:iam::<account-id>:oidc-provider/idp.hashicorp.com/oidc/organization/<org-id>"
    dynamic_secret_role_arn = "arn:aws:iam::<account-id>:role/hcp-vault-secrets-dynamic-secret"
    integration_name = "<integration-name>"
    integration_role_arn = "arn:aws:iam::<account-id>:role/hcp-vault-secrets-integration"
    ```

  </CodeBlockConfig>

</Tab>
<Tab heading="Terraform - Access Keys">

The Terraform [AWS
provider](https://registry.terraform.io/providers/hashicorp/aws/latest) can be
used to provision the necessary resources.

1. Follow the [Terraform
   documentation](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#authentication-and-configuration)
   to authenticate the AWS provider.

1. Use your preferred text editor and create a Terraform configuration file with the following snippet:

  ```hcl
  provider "aws" {}
  
  resource "aws_iam_user" "integration_user" {
    name = "hcp-vault-secrets-integration"
  }
  
  resource "aws_iam_access_key" "integration_user_access_key" {
    user = aws_iam_user.integration_user.name
  }
  
  resource "aws_iam_role" "dynamic_secret_role" {
    name = "hcp-vault-secrets-dynamic-secret"
    assume_role_policy = jsonencode({
      "Version" : "2012-10-17",
      "Statement" : [
        {
          "Effect" : "Allow",
          "Action" : "sts:AssumeRole",
          "Principal" : {
            "AWS" : aws_iam_user.integration_user.arn
          },
          "Condition" : {}
        }
      ]
    })
  
    inline_policy {
      name   = "hcp-vault-secrets-s3-lister"
      policy = data.aws_iam_policy_document.dynamic_secret_policy.json
    }
  }
  
  data "aws_iam_policy_document" "dynamic_secret_policy" {
    # Replace with the permissions to grant to your dynamic secret
    statement {
      effect = "Allow"
      actions = [
        "s3:List*"
      ]
      resources = ["*"]
    }
  }
  
  output "integration_access_key_id" {
    value = aws_iam_access_key.integration_user_access_key.id
  }
  
  // Note that this will be written to the state file. 
  // If you use this, please protect your backend state file.
  // https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/iam_access_key#secret
  output "integration_secret_access_key" {
    value = aws_iam_access_key.integration_user_access_key.secret
    sensitive = true
  }
  
  output "dynamic_secret_role_arn" {
    value = aws_iam_role.dynamic_secret_role.arn
  }
  ```

1. Initialize Terraform.

  ```shell-session
  $ terraform init
  ```

  This will prepare a working directory:

  <CodeBlockConfig hideClipboard>

    ```plaintext
    ...snip...

    Terraform has been successfully initialized!
    ```
  </CodeBlockConfig>

1. Generate your AWS IAM configuration with the apply command:

  ```shell-session
  $ terraform apply
  ```

  Review the actions Terraform is about to perform and say `yes` to accept the plan:

  <CodeBlockConfig hideClipboard>

    ```plaintext
    ...snip...

      Plan: 3 to add, 0 to change, 0 to destroy.
      
      Changes to Outputs:
      + dynamic_secret_role_arn       = (known after apply)
      + integration_access_key_id     = (known after apply)
      + integration_secret_access_key = (sensitive value)

    Do you want to perform these actions?
    Terraform will perform the actions described above.
    Only 'yes' will be accepted to approve.

    Enter a value: yes
    ```

  </CodeBlockConfig>

1. Keep the Terraform outputs on hand for the next steps:

  <CodeBlockConfig hideClipboard>

    ```plaintext
    Apply complete! Resources: 3 added, 0 changed, 0 destroyed.

    Outputs:

    dynamic_secret_role_arn = "arn:aws:iam::<account-id>:role/hcp-vault-secrets-dynamic-secret"
    integration_access_key_id = "AKIA..."
    integration_secret_access_key = <sensitive>
    ```

  </CodeBlockConfig>

</Tab>
</Tabs>

## Configure dynamic secrets

1. Navigate to the [Vault Secrets app
   panel](https://portal.cloud.hashicorp.com/services/secrets/apps) and select
   an app where you want to create a dynamic secret.

1. Click **Create new secret** and select **Dynamic secret**.

1. Select the **AWS** option from the pull down menu.

1. Select an existing integration or select **Add new integration**.

1. Select an **Authentication method** and follow the appropriate steps below.
   ![Setup new aws_dynamic_integration](/img/docs/vault-secrets/create-aws-dyn-integration.png)

<Tabs>
<Tab heading="OIDC Federation">

1. Provide a unique **Integration Name** for this integration.

1. Use the **AWS IAM identity provider audience** configured during the previous
   steps. The format is `arn:aws:iam::<account-id>:oidc-provider/idp.hashicorp.com/oidc/organization/<org-id>`.

1. Use the **Integration AWS IAM role ARN** configured during the previous
   steps. This role is used by HCP to manage credentials.

1. Click **Add new integration** to return to the new secret form.

   <Note>

   If you encounter an error, make sure the audience matches between HCP and AWS. Also verify the condition on the trust
   relationship for the AWS IAM role corresponds to your HCP organization ID, project ID and integration name.

   </Note>

</Tab>
<Tab heading="Access Keys">

1. Provide a unique **Integration Name** for this integration.

1. Use the **Access key ID** configured for the IAM user during the previous steps.

1. Use the **Secret access key** configured for the IAM user during the previous steps.

1. Click **Add new integration** to return to the new secret form.

</Tab>
</Tabs>

### Add new dynamic secret

1. Provide a unique **Secret Name** for this secret.
   ![Setup new aws_dynamic_secret](/img/docs/vault-secrets/create-aws-dyn-secret.png)

1. Use the **dynamic secret AWS IAM role ARN** configured during the previous
   steps.

1. Select a **Time to Live (TTL)** for the generated dynamic credentials between 15 minutes 
   and 12 hours. Note that the upper limit may vary based on your AWS IAM’s role’s maximum 
   session duration, which is typically 1 hour.

## Accessing dynamic credentials

A dynamic secret is a template to generate dynamic credentials on demand. Each
time a dynamic secret is accessed, a new credentials set is generated and shared
exclusively with the requesting client. Dynamic credentials can be generated using:

- [HCP portal](https://portal.cloud.hashicorp.com/services/secrets)

  ![Generate new aws STS credentials](/img/docs/vault-secrets/generate-dyn-creds-aws.png)

- [HCP API](https://developer.hashicorp.com/hcp/api-docs/vault-secrets/2023-11-28#OpenAppSecret)

  ```shell-session
  curl \
  --location "https://api.cloud.hashicorp.com/secrets/2023-11-28/organizations/${HCP_ORG_ID}/projects/${HCP_PROJ_ID}/apps/${APP_NAME}/secrets/${SECRET_NAME}:open" \
  --request GET \
  --header "Authorization: Bearer ${HCP_API_TOKEN}" | jq
  ```

- [HCP CLI](https://developer.hashicorp.com/hcp/docs/cli)

  ```shell-session
  hcp vault-secrets secrets open ${SECRET_NAME}
  ```

- [HCP Terraform provider](https://registry.terraform.io/providers/hashicorp/hcp/latest/docs/data-sources/vault_secrets_dynamic_secret)

  ```hcl
  data "hcp_vault_secrets_dynamic_secret" "my_dynamic_secret" {
    app_name    = "my-app"
    secret_name = "my_dynamic_secret"
  }
  ```