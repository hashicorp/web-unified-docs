---
page_title: Prevent cloud cost overruns with automated budgets and spending alerts
description: Prevent cloud cost overruns by automating budget alerts in AWS, Azure, and GCP with Terraform. Get notified before spending exceeds limits.
---

# Create cloud budgets with Terraform

Cloud spending can quickly get out of control without proper oversight. According to the [2023 HashiCorp State of Cloud Strategy Survey](https://www.hashicorp.com/en/blog/hashicorp-state-of-cloud-strategy-survey-2023-maturity-drives-operational-efficiency), 94% of respondents experienced avoidable cloud costs. Terraform lets you automate cloud budget creation and spending alerts across AWS, Azure, and Google Cloud, giving you visibility and control before costs exceed thresholds.

## Why create cloud budgets

Cloud budgets address the following challenges:

- **Lack of spending visibility:** Without defined budgets, you cannot track where cloud resources consume your budget or identify which teams or projects drive costs.
- **Reactive cost management:** Discovering cost overruns after they occur leaves you with unexpected bills and no time to take corrective action before the billing cycle ends.
- **Missed spending anomalies:** Manual cost reviews cannot detect unusual spending patterns in real-time, allowing misconfigured resources or unauthorized usage to accumulate charges unnoticed.
- **Poor financial forecasting:** Without historical budget data and spending trends, you cannot accurately predict future cloud costs or allocate budgets effectively across teams.

## How cloud budgets work

Cloud budgets let you set spending thresholds and receive alerts when actual or forecasted costs approach those limits. Most major cloud providers offer native budget tools that integrate with Terraform:

- **AWS Budgets:** Track costs by service, linked account, or tag. Supports actual and forecasted spending alerts.
- **Azure Cost Management:** Create subscription or resource group-level budgets with threshold notifications.
- **Google Cloud Budgets:** Monitor spending at the billing account level with project and label filtering.

Using Terraform to manage budgets provides consistency across environments and enables you to version control your cost governance policies alongside your infrastructure.

## Terraform budget configuration with notification alerts

The following examples show cloud budgets that send alerts at 80% actual spending and 100% forecasted spending:

<Tabs>
<Tab heading="AWS" group="cloud-provider">

```hcl
resource "aws_budgets_budget" "monthly" {
  name              = "budget-ec2-monthly"
  budget_type       = "COST"
  limit_amount      = "1200"
  limit_unit        = "USD"
  time_unit         = "MONTHLY"
  time_period_start = "2024-01-01_00:00"

  cost_filter {
    name   = "Service"
    values = ["Amazon Elastic Compute Cloud - Compute"]
  }

  notification {
    comparison_operator        = "GREATER_THAN"
    threshold                  = 80
    threshold_type             = "PERCENTAGE"
    notification_type          = "ACTUAL"
    subscriber_email_addresses = ["platform-team@example.com"]
  }

  notification {
    comparison_operator        = "GREATER_THAN"
    threshold                  = 100
    threshold_type             = "PERCENTAGE"
    notification_type          = "FORECASTED"
    subscriber_email_addresses = ["platform-team@example.com"]
  }
}
```

</Tab>
<Tab heading="Azure" group="cloud-provider">

```hcl
resource "azurerm_consumption_budget_subscription" "monthly" {
  name            = "monthly-budget"
  subscription_id = data.azurerm_subscription.current.id
  amount          = 1200
  time_grain      = "Monthly"

  time_period {
    start_date = "2024-01-01T00:00:00Z"
  }

  notification {
    enabled        = true
    threshold      = 80
    operator       = "GreaterThan"
    contact_emails = ["platform-team@example.com"]
  }

  notification {
    enabled        = true
    threshold      = 100
    operator       = "GreaterThan"
    threshold_type = "Forecasted"
    contact_emails = ["platform-team@example.com"]
  }
}
```

</Tab>
<Tab heading="GCP" group="cloud-provider">

```hcl
resource "google_billing_budget" "monthly" {
  billing_account = var.billing_account_id
  display_name    = "Monthly Budget"

  budget_filter {
    projects = ["projects/${var.project_id}"]
  }

  amount {
    specified_amount {
      currency_code = "USD"
      units         = 1200
    }
  }

  threshold_rules {
    threshold_percent = 0.8
    spend_basis       = "CURRENT_SPEND"
  }

  threshold_rules {
    threshold_percent = 1.0
    spend_basis       = "FORECASTED_SPEND"
  }
}
```

</Tab>
</Tabs>

Each configuration creates a monthly budget with two notification thresholds: an early warning at 80% of actual spending and a forecasted alert at 100% of projected costs. The AWS example filters by EC2 service, while the Azure and GCP examples track subscription or project-level spending. Refer to the Terraform registry documentation for complete configuration options.

## Respond to budget alerts

When you receive a budget alert, complete the following steps:

1. **Identify the cause:** Use your cloud provider's cost explorer to examine which resources or usage types increased.
2. **Take action:** [Decommission forgotten resources](/well-architected-framework/optimize-systems/lifecycle-management/decommission-infrastructure), adjust autoscaling policies, or investigate potential security incidents.
3. **Update thresholds:** If the increase is legitimate growth, adjust your budget configuration and apply the changes with Terraform.

For automated detection of unusual spending patterns that fall within budget limits, implement [spending anomaly detection](/well-architected-framework/optimize-systems/manage-cost/detect-cloud-spending-anomalies).

## Resources

Terraform registry:

- Read the [aws_budgets_budget](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/budgets_budget) documentation for cost filters, notification types, and SNS integration
- Read the [azurerm_consumption_budget_subscription](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/consumption_budget_subscription) documentation for subscription and resource group budget options
- Read the [google_billing_budget](https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/billing_budget) documentation for threshold rules and Pub/Sub notifications

Cloud provider documentation:

- Learn about [AWS Budgets](https://docs.aws.amazon.com/cost-management/latest/userguide/budgets-managing-costs.html) for budget types, actions, and cost allocation tags
- Learn about [Azure Cost Management budgets](https://learn.microsoft.com/en-us/azure/cost-management-billing/costs/tutorial-acm-create-budgets) for scope options and action groups
- Learn about [GCP billing budgets](https://cloud.google.com/billing/docs/how-to/budgets) for programmatic notifications and budget alerts

## Next steps

- Implement [spending anomaly detection](/well-architected-framework/optimize-systems/manage-cost/detect-cloud-spending-anomalies) to catch unusual patterns that budgets miss.
- Establish a [cloud resource tagging strategy](/well-architected-framework/optimize-systems/lifecycle-management/tag-cloud-resources) for granular cost attribution.
- Get started with [Terraform fundamentals tutorials](/terraform/tutorials) for hands-on infrastructure as code examples.